plugins {
    id 'net.neoforged.moddev' version '1.0.20'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://maven.blamejared.com/' }
}

sourceSets {
    api
    data
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

dependencies {
    implementation sourceSets.api.output
    dataImplementation sourceSets.api.output
    dataImplementation sourceSets.main.output
    testImplementation "org.junit.jupiter:junit-jupiter:${project.junit_version}"
    testImplementation "net.neoforged:testframework:${project.neo_version}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    compileOnly "mezz.jei:jei-${mc_version}-neoforge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${mc_version}-neoforge:${jei_version}"
}

neoForge {
    version = project.neo_version
    addModdingDependenciesTo(sourceSets.api)
    addModdingDependenciesTo(sourceSets.data)

    mods {
        "${mod_id}" {
            sourceSet sourceSets.api
            sourceSet sourceSets.main
        }
        "${mod_id}data" {
            sourceSet sourceSets.api
            sourceSet sourceSets.main
            sourceSet sourceSets.data
        }
    }

    runs {
        configureEach {
            systemProperty 'forge.logging.console.level', 'debug'
        }
        client {
            client()
        }
        data {
            data()
            sourceSet = sourceSets.data
            mods = [neoForge.mods."${mod_id}data"]
            programArguments.addAll '--mod', "${mod_id}", '--all',
                    '--output',   file('src/generated/resources/').getAbsolutePath(),
                    '--existing', file('src/main/resources/').getAbsolutePath()
        }
        server {
            server()
            programArgument '--nogui'
        }
    }

    unitTest {
        enable()
        testedMod = mods."${mod_id}"
    }
}

tasks.named('processResources', ProcessResources) {
    var replaceProperties = [
            mc_version       : mc_version,
            mc_version_range : mc_version_range,
            neo_version      : neo_version,
            neo_version_range: neo_version_range,
            loader_version   : loader_version,
            mod_id           : mod_id,
            mod_version      : mod_version,
            mod_name         : mod_name,
            mod_authors      : mod_authors,
            mod_credits      : mod_credits,
            mod_logo         : mod_logo,
            mod_description  : mod_description,
            mod_license      : mod_license,
    ]
    inputs.properties replaceProperties
    filesMatching('META-INF/neoforge.mods.toml') {
        expand replaceProperties
    }
}

test {
    useJUnitPlatform()
}

jar {
    from(sourceSets.api.output)
    from(sourceSets.main.output)
}

tasks.register('apiJar', Jar) {
    archiveClassifier.set('api')
    from(sourceSets.api.output)
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    from sourceSets.api.allSource
}

tasks.register('apiSourcesJar', Jar) {
    archiveClassifier.set('api-sources')
    from sourceSets.api.allSource
}

tasks.withType(Jar).configureEach {
    archiveBaseName = mod_id
    archiveVersion = "${mc_version}-${mod_version}"
    processResources.exclude('**/.cache/**')
    processResources.exclude('**/blockbench/**')
}

artifacts {
    archives apiJar
    archives sourcesJar
    archives apiSourcesJar
}
